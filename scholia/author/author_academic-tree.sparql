PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#defaultView:Graph
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# Doctoral student/advisor network with a source from a spectific researcher
PREFIX gas: <http://www.bigdata.com/rdf/gas#>

SELECT DISTINCT ?student1 ?student1Label ?rgb ?supervisor1 ?supervisor1Label
WHERE {
  { 
    SELECT ?student1 ?supervisor1 (MIN(?depth1) as ?depth)
    WHERE {
      SERVICE gas:service {
        gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
                gas:in target: ;
                gas:traversalDirection "Forward" ;
                gas:out ?student1 ;
                gas:out1 ?depth1 ;
                gas:out2 ?supervisor1 ;
                gas:linkType wdt:P185 ;
      }
    }
    GROUP BY ?student1 ?supervisor1
  }
  UNION
  { 
    SELECT ?student1 ?supervisor1 (MIN(?depth1) as ?depth)
    WHERE {
      service gas:service {
        gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
                gas:in target: ;
                gas:traversalDirection "Forward" ;
                gas:out ?supervisor1 ;
                gas:out1 ?depth1 ;
                gas:out2 ?student1 ;
                gas:linkType wdt:P184 ;
      }
    }
    GROUP BY ?student1 ?supervisor1
  }
  UNION
  { 
    SELECT ?student1 ?supervisor1 (MIN(?depth1) as ?depth)
    WHERE {
      SERVICE gas:service {
        gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
                gas:in target: ;
                gas:traversalDirection "Reverse" ;
                gas:out ?student1 ;
                gas:out1 ?depth1 ;
                gas:out2 ?supervisor1 ;
                gas:linkType wdt:P184 ;
      }
    }
    GROUP BY ?student1 ?supervisor1
  }
  UNION
  { 
    SELECT ?student1 ?supervisor1 (MIN(?depth1) as ?depth)
    WHERE {
      SERVICE gas:service {
        gas:program gas:gasClass "com.bigdata.rdf.graph.analytics.BFS" ;
                gas:in target: ;
                gas:traversalDirection "Reverse" ;
                gas:out ?supervisor1 ;
                gas:out1 ?depth1 ;
                gas:out2 ?student1 ;
                gas:linkType wdt:P185 ;
      }
    }
    GROUP BY ?student1 ?supervisor1
  }
  
  BIND( IF( ?student1 = target:, "3080BB", "ffffff") AS ?rgb)

  OPTIONAL { ?student1 rdfs:label ?student1Label .  FILTER ( lang(?student1Label) = 'en' ) }
  OPTIONAL { ?supervisor1 rdfs:label ?supervisor1Label .  FILTER ( lang(?supervisor1Label) = 'en' ) } 
}

