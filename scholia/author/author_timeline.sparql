PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#defaultView:Timeline
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# Timeline for an author
SELECT ?label ?beginning ?ending ?education_degree_label
WHERE {
  # { Birth target: wdt:P569 ?beginning . BIND("Birth" AS ?label) } UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        {
  
  SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
    {
  SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
    ?work wdt:P50 target: .
    ?work wdt:P577 ?publication_date . 
  }
  GROUP BY ?work
}   OPTIONAL { ?citing_article wdt:P2860 ?work . }
  }
  GROUP BY ?work ?beginning 
}     }
      ORDER BY DESC(?number_of_citations)
      LIMIT 1        
    }
    BIND("üìñ publication of most cited article" AS ?label)
  }
  UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        {
  
  SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
    {
  SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
    ?work wdt:P50 target: .
    ?work wdt:P577 ?publication_date . 
  }
  GROUP BY ?work
}   OPTIONAL { ?citing_article wdt:P2860 ?work . }
  }
  GROUP BY ?work ?beginning 
}     }
      ORDER BY ?beginning
      LIMIT 1        
    }
    BIND("üìñ first publication" AS ?label)
  }
  UNION
  {
    { 
      SELECT ?work ?beginning WHERE {
        {
  
  SELECT ?work ?beginning (COUNT(?citing_article) AS ?number_of_citations) WHERE {
    {
  SELECT ?work (MIN(?publication_date) AS ?beginning) WHERE {
    ?work wdt:P50 target: .
    ?work wdt:P577 ?publication_date . 
  }
  GROUP BY ?work
}   OPTIONAL { ?citing_article wdt:P2860 ?work . }
  }
  GROUP BY ?work ?beginning 
}     }
      ORDER BY DESC(?beginning)
      LIMIT 1        
    }
    BIND("üìñ latest publication" AS ?label)
  }
  UNION
  {
    # Education
    target: p:P69 ?education_statement .
    ?education_statement ps:P69 ?education .
    ?education rdfs:label ?education_label . 
    FILTER (lang(?education_label) = 'en')
    BIND(CONCAT("ü¶â ", ?education_label) AS ?label)
    OPTIONAL { ?education_statement pq:P580 ?beginning . }
    OPTIONAL { ?education_statement pq:P582 ?ending . }
    OPTIONAL {
      ?education_statement pq:P512 ?education_degree . 
      ?education_degree rdfs:label ?education_degree_label .
      FILTER (lang(?education_degree_label) = 'en')
    }
  }
  UNION
  {
    # Affiliation
    target: p:P1416 | p:P108 ?affiliation_statement .
    ?affiliation_statement ps:P1416 | ps:P108 ?affiliation .
    ?affiliation rdfs:label ?affiliation_label .
    FILTER (lang(?affiliation_label) = 'en')
    BIND(CONCAT("üè† ",?affiliation_label) AS ?label)
    # OPTIONAL { ?affiliation wdt:P18 ?image }
    OPTIONAL { ?affiliation_statement pq:P580 ?beginning . }
    OPTIONAL { ?affiliation_statement pq:P582 ?ending . }
    OPTIONAL {
      ?affiliation_statement pq:P512 ?affiliation_degree . 
      ?affiliation_degree rdfs:label ?affiliation_degree_label .
      FILTER (lang(?affiliation_degree_label) = 'en')
    }
  }
  UNION
  {
    target: p:P166 ?award_statement .
    ?award_statement ps:P166 ?award .
    ?award rdfs:label ?award_label .
    FILTER (lang(?award_label) = 'en')
    BIND(CONCAT("üèÜ ",?award_label) AS ?label)
    ?award_statement pq:P585 ?beginning .
  }
  UNION
  {
    target: wdt:P569 ?beginning .
    BIND(CONCAT("üë∂ born ") AS ?label) .
  }
  UNION
  {
    target: wdt:P570 ?beginning .
    BIND(CONCAT("ü•Ä died") AS ?label) .
  }
			    
} 
