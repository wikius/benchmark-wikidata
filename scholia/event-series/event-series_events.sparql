PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

# title: List of event and proceedings for a specific event series
SELECT DISTINCT 
  (SAMPLE(?years) AS ?year)
  (SAMPLE(?ordinali) AS ?ordinal)
  (SAMPLE(?short_names) AS ?short_name)
  ?event ?eventLabel (CONCAT("/event/", SUBSTR(STR(?event), 32)) AS ?eventUrl)
  ?proceedings ?proceedingsLabel (CONCAT("/venue/", SUBSTR(STR(?proceedings), 32)) AS ?proceedingsUrl)
WHERE {
  {
    ?event wdt:P179 | wdt:P31 target: .
    OPTIONAL {
      ?event wdt:P585 | wdt:P580 ?datetime .
      BIND(YEAR(?datetime) AS ?years)
    }
    OPTIONAL {
      ?event wdt:P1813 ?short_names
    }
    OPTIONAL {
      ?proceedings wdt:P4745 ?event
    }
    OPTIONAL{
      ?event p:P179/pq:P1545 ?ordinali
    }
  }
  UNION
  {
    # proceedings even if the event is not there!!!
    ?proceedings ( wdt:P179 / wdt:P4745 ) target: .
    OPTIONAL {
      ?proceedings wdt:P577 ?datetime .
      BIND(YEAR(?datetime) AS ?years)    
    }
  }
  OPTIONAL { ?event rdfs:label ?eventLabel .  FILTER ( lang(?eventLabel) = 'en' ) }
  OPTIONAL { ?proceedings rdfs:label ?proceedingsLabel .  FILTER ( lang(?proceedingsLabel) = 'en' ) }
  }
GROUP BY ?event ?year ?eventLabel ?proceedings ?proceedingsLabel
ORDER BY DESC(?year)
