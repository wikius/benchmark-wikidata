PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#defaultView:Graph

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT 
  ?author1 ?author1Label (SAMPLE(?image1_) AS ?image1) ?rgb
  ?author2 ?author2Label (SAMPLE(?image2_) AS ?image2)
WHERE {
  {
  SELECT DISTINCT ?author1 ?author2 ?rgb WHERE {
    {
  SELECT
    ?author1
    (SAMPLE(?dark_rgb_) AS ?dark_rgb)
    (SAMPLE(?light_rgb_) AS ?light_rgb)
  WHERE {
    # Find people at the event
    { 
      target: (wdt:P823 | wdt:P664 | ^wdt:P1344 | wdt:P710) ?author1 .
      BIND ("EEEEEE" AS ?dark_rgb_)
    }
    UNION 
    {
      # author of article in proceedings, or program committee member
      target:  (^wdt:P4745 / ^wdt:P1433 / wdt:P50) | wdt:P5804 ?author1 .
      BIND ("FFFFFF" AS ?light_rgb_)
    }
    UNION
    {
      # author of article in proceedings, or program committee member
      ?presentation wdt:P823 ?author1;
      wdt:P5072 target:
      BIND ("FFFFFF" AS ?light_rgb_)
   }
  }
  GROUP BY ?author1
}   {        
    target: (wdt:P823 | wdt:P664 | ^wdt:P1344 | wdt:P710 | ^wdt:P4745 / ^wdt:P1433 / wdt:P50)  ?author2 .}
    UNION
    { ?presentation wdt:P823 ?author2; wdt:P5072 target:    }
            
    # Find co-authors
    ?work wdt:P50 ?author1 , ?author2 .
    FILTER (?author1 != ?author2)

    BIND(COALESCE(?dark_rgb, ?light_rgb) AS ?rgb)
  }
}   
  OPTIONAL { ?author1 wdt:P18 ?image1_ . }
  OPTIONAL { ?author2 wdt:P18 ?image2_ . }
  OPTIONAL { ?author2 rdfs:label ?author2Label .  FILTER ( lang(?author2Label) = 'en' ) }
  OPTIONAL { ?author1 rdfs:label ?author1Label .  FILTER ( lang(?author1Label) = 'en' ) }
}
GROUP BY ?author1 ?author1Label ?rgb ?author2 ?author2Label  
