PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  ?description ?descriptionLabel ?descriptionUrl

  ?value ?valueLabel ?valueUrl

WHERE {
  {
    BIND(1 AS ?order)
    BIND("Event series" AS ?description)
    BIND("/event-series" AS ?descriptionUrl)
    target: wdt:P179 ?value .
    BIND(CONCAT("/event-series/", SUBSTR(STR(?value), 32)) AS ?valueUrl)
  }
  UNION
  {
    BIND(2 AS ?order)
    BIND("Short name" AS ?description)
    target: wdt:P1813 ?value .
  }
  UNION
  {
    BIND(5 AS ?order)
    BIND(wd:Q34770 AS ?description)
    BIND("/language" AS ?descriptionUrl)
    
    target: wdt:P2936 ?value .
    BIND(CONCAT("/language/", SUBSTR(STR(?value), 32)) AS ?valueUrl)
  }
  UNION
  {
    SELECT
      (10 AS ?order)
      ("Organizers" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("/authors/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)      
    {
      BIND(1 AS ?dummy)
      target: wdt:P664 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string .
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(20 AS ?order)
    BIND("Number of participants" AS ?description)
    
    target: wdt:P1132 ?value .
  }
  UNION
  {
    # number of submissions
    BIND(21 AS ?order)
    BIND("number of submissions"  AS ?description)
    
    target: p:P12655 ?value_statement .
    ?value_statement ps:P12655 ?submissions.
    OPTIONAL {
      ?value_statement  pq:P518 / rdfs:label ?track .
      FILTER (LANG(?track) = "en")
    }
    BIND(IF(BOUND(?track), CONCAT(STR(?submissions), " (", ?track, ")"), STR(?submissions)) AS ?value)
  }
  UNION
  {
    # number of accepted contributions
    BIND(22 AS ?order)
    BIND("number of accepted contributions"  AS ?description)
    
    target: p:P12649 ?value_statement .
    ?value_statement ps:P12649 ?accepted .
    OPTIONAL {
      ?value_statement  pq:P518 / rdfs:label ?track .
      FILTER (LANG(?track) = "en")
    }
    BIND(IF(BOUND(?track), CONCAT(STR(?accepted), " (", ?track, ")"), STR(?accepted)) AS ?value)
  }
  UNION
  {
    BIND(23 AS ?order)
    BIND("Acceptance&nbsp;rate" AS ?description)
    
    target: p:P5822 ?value_statement .
    ?value_statement ps:P5822 ?acceptance_rate .
    OPTIONAL {
      ?value_statement  pq:P518 / rdfs:label ?track .
      FILTER (LANG(?track) = "en")
    }
    BIND(IF(BOUND(?track), CONCAT(STR(?acceptance_rate * 100), " % (", ?track, ")"), CONCAT(STR(?acceptance_rate * 100), " %")) AS ?value)
  }

  OPTIONAL { ?value rdfs:label ?valueLabel .  FILTER ( lang(?valueLabel) = 'en' ) }
  OPTIONAL { ?description rdfs:label ?descriptionLabel .  FILTER ( lang(?descriptionLabel) = 'en' ) }
} 
ORDER BY ?order
