PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  (?number_of_publications AS ?works)
  ?person ?personLabel ?personDescription
  ?roles
  ?example_work ?example_workLabel
 
WHERE {
  {
  SELECT
    (COUNT(?work) AS ?number_of_publications)
    ?roles
    ?person (SAMPLE(?work) AS ?example_work)
  WHERE {
    {
  SELECT
    (GROUP_CONCAT(DISTINCT ?role; separator=", ") AS ?roles) ?person
  WHERE {
    {
      # speaker added directly in the target item
      target: wdt:P823 ?person .
      # BIND(wd:Q9379869 AS ?role)
      BIND("speaker" AS ?role)
    }
    UNION
    {
      # speaker inferred from presentation items related to the event-target
      ?presentation wdt:P823 ?person; wdt:P5072 target: 
      BIND("speaker" AS ?role)
    }
    UNION
    {
      # organizer
      target: wdt:P664 ?person .
      #BIND(wd:Q2029941 AS ?role)
      BIND("organizer" AS ?role)
    }
    UNION
    {
      # participant
      ?person wdt:P1344 | ^wdt:P710 target: .
      BIND("participant" AS ?role)
    }
    UNION
    {
      # editor
      ?person ^wdt:P98 / wdt:P4745 target: .
      BIND("editor of proceedings" AS ?role)
    }
    UNION
    {
      # author
      ?person ^wdt:P50 / wdt:P1433 / wdt:P4745 target: .
      BIND("author" AS ?role)
    }
    UNION
    {
      # program committee member
      target: wdt:P5804 ?person .
      BIND("program committee member" AS ?role)
    }
  }
  GROUP BY ?person
}
    ?person wdt:P31 wd:Q5.
    OPTIONAL { ?work wdt:P50 ?person . }
  }
  GROUP BY ?roles ?person
} OPTIONAL { ?person rdfs:label ?personLabel .  FILTER ( lang(?personLabel) = 'en' ) }
  OPTIONAL { ?example_work rdfs:label ?example_workLabel .  FILTER ( lang(?example_workLabel) = 'en' ) }
}
ORDER BY DESC(?number_of_publications)
LIMIT 10000
