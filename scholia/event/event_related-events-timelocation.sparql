PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT
  (ROUND(1000 * ?score_) / 1000 AS ?score)
  (ROUND(1000 * ?time_score_) / 1000 AS ?time_score)
  (ROUND(1000 * ?location_score_) / 1000 AS ?location_score) 
  ?event ?eventLabel
WHERE {
  {
  SELECT
    (SUM(?scores) AS ?score_)
    (MAX(?time_scores) AS ?time_score_)
    (MAX(?location_scores) AS ?location_score_)
    ?event
  WHERE {
    {
  SELECT ?event WHERE {
    {
  SELECT ?event_type WHERE {
    VALUES ?event_type { wd:Q2020153 wd:Q46855 wd:Q40444998 }
  }
}   ?event wdt:P31 / wdt:P279* ?event_type .
  }
}          
    { 
      # Around the same time
      target: wdt:P580 | wdt:P585 ?time0 .
      ?event wdt:P580 | wdt:P585 ?time .
      BIND((365.25 * YEAR(?time0) + 30.5 * MONTH(?time0) + DAY(?time0)) AS ?day0)
      BIND((365.25 * YEAR(?time) + 30.5 * MONTH(?time) + DAY(?time)) AS ?day)
      
      # TODO: multiple days cause double counting
      BIND(20 / (1 + ABS(?day - ?day0)) AS ?time_scores)
    }
    # UNION
    {
      SELECT ?event (MAX(?inverse_distance) AS ?location_scores) WHERE {
        {
  SELECT ?event WHERE {
    {
  SELECT ?event_type WHERE {
    VALUES ?event_type { wd:Q2020153 wd:Q46855 wd:Q40444998 }
  }
}   ?event wdt:P31 / wdt:P279* ?event_type .
  }
}                
        # geocoordinate of the query event
        target: wdt:P276? / wdt:P159? / wdt:P625 ?geo0 .
        
        # geocoordinate of other events
        ?event wdt:P276? / wdt:P625 ?geo .
        
        # inverse distance
        BIND((200 / (1 + geof:distance(?geo, ?geo0))) AS ?inverse_distance)
      }
      GROUP BY ?event
    }

    # Should online event be listed? 
    # UNION
    # {
    #   VALUES ?online_event { wd:Q1543677 wd:Q98381855 wd:Q98381912 }
    #   ?event wdt:P31 ?online_event .
    #   BIND(200 AS ?location_scores)
    # }
  
    BIND((?time_scores * ?location_scores) AS ?scores)
  }
  GROUP BY ?event
  
} FILTER (?event != target:)
  OPTIONAL { ?event rdfs:label ?eventLabel .  FILTER ( lang(?eventLabel) = 'en' ) }
}
ORDER BY DESC(?score)
