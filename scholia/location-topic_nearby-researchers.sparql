PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target1: <http://www.wikidata.org/entity/{{ q1 }}>
PREFIX target2: <http://www.wikidata.org/entity/{{ q2 }}>

# title: Nearby researchers that work with a specific topic and near a specified geographical entity
SELECT
  ?score
  ?author ?authorLabel (CONCAT("/author/", SUBSTR(STR(?author), 32)) AS ?authorUrl)
  ?example_work ?example_workLabel (CONCAT("/work/", SUBSTR(STR(?example_work), 32)) AS ?example_workUrl)
WHERE {
  {
  SELECT
    ?author
    (SUM(?topic_score) * MAX(?inverse_distance) AS ?score)
    (SAMPLE(?work) AS ?example_work)
  WHERE {
    {
  # Compute distance for authors
  SELECT
    (MAX(?inverse_distance_) AS ?inverse_distance)
    ?author
  WHERE {
    {
  # Academic institutions on all levels
  SELECT DISTINCT ?organization ?other_geo ?distance WHERE {
    {
  # Universities, research centers, etc.
  SELECT ?university WHERE {
    VALUES ?university { wd:Q3918 wd:Q1371037 wd:Q7315155 wd:Q31855 wd:Q2385804 }
  }
}   {
  SELECT ?organization ?distance ?other_geo {
    target1: wdt:P625 ?geo .
    SERVICE wikibase:around { 
      ?organization wdt:P625 ?other_geo .
      bd:serviceParam wikibase:center ?geo .
      bd:serviceParam wikibase:radius "50" .
      bd:serviceParam wikibase:distance ?distance .
    }
  }
}           
    ?organization wdt:P361* / wdt:P31 / wdt:P279* ?university .
  }
}    BIND(1 / ?distance AS ?inverse_distance_)
    ?author wdt:P108 | wdt:P1416 ?organization .  
  }
  GROUP BY ?author
}   {
      ?work wdt:P50 ?author .
      ?work wdt:P921 / wdt:P279* target2: . BIND(3 AS ?topic_score)
    }
    UNION
    { ?author wdt:P101 target2: . BIND(20 AS ?topic_score) }
  }
  GROUP BY ?author
} OPTIONAL { ?example_work rdfs:label ?example_workLabel .  FILTER ( lang(?example_workLabel) = 'en' ) }
  OPTIONAL { ?author rdfs:label ?authorLabel .  FILTER ( lang(?authorLabel) = 'en' ) }
}
ORDER BY DESC(?score) 
