PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#defaultView:BarChart

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?year (SUM(?citations_per_author_) AS ?citations_per_author) ?researcherLabel

WHERE {
  # Label the results
  {
  # Find works of the researchers and count the number of citations
  SELECT
    ?researcher ?work ?year (COUNT(DISTINCT ?citing_work) / COUNT(DISTINCT ?researcher_of_paper) AS ?citations_per_author_)
  WHERE {
    {
  # Find researchers affiliated with the organization
  SELECT DISTINCT ?researcher WHERE {
    ?researcher wdt:P108 | wdt:P463 | wdt:P1416/wdt:P361* target: .
  }
}   ?work wdt:P50 | wdt:P2093 ?researcher_of_paper .
    ?work wdt:P50 ?researcher .
    ?citing_work wdt:P2860 ?work .
    ?work wdt:P577 ?date . 
    BIND(STR(YEAR(?date)) AS ?year)
  }
  GROUP BY ?work ?researcher ?year
}  
  OPTIONAL { ?researcher rdfs:label ?researcherLabel .  FILTER ( lang(?researcherLabel) = 'en' ) }
}
GROUP BY ?year ?researcher ?researcherLabel
ORDER BY ?year
