PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT ?date ?work ?workLabel ?type ?topics
WHERE {
  {
  SELECT
    (MAX(?dates) as ?datetime)
    ?work
    (GROUP_CONCAT(DISTINCT ?type_label; separator=", ") AS ?type)
    (GROUP_CONCAT(?topic_label; separator=" // ") AS ?topics)
  WHERE {
    {
  SELECT DISTINCT ?work WHERE {
    {
      ?work wdt:P921 / (wdt:P527+ | wdt:P1269+ | (wdt:P31* / wdt:P279*) ) target: .
    }
    UNION
    {
      # Works funded by the project, - recursively
      ?work wdt:P859+ target: .
    }
    UNION
    {
      # Works funded by the project, - recursively
      ?work wdt:P859/(wdt:P1344|^wdt:P710)* target: .
    }
    UNION {
      target: ?propp ?statement .
      ?statement a wikibase:BestRank ;
                 prov:wasDerivedFrom/pr:P248 ?work .
    }
  }
}   OPTIONAL {
       ?work wdt:P31 ?type_ .
       ?type_ rdfs:label ?type_label . FILTER (LANG(?type_label) = 'en')
    }
    OPTIONAL { ?work wdt:P577 ?dates . }
    OPTIONAL {
      ?work wdt:P921 ?topic .
      ?topic rdfs:label ?topic_label . FILTER (lang(?topic_label) = 'en')
    }
  }
  GROUP BY ?work
} BIND((?datetime) AS ?date)
  OPTIONAL { ?work rdfs:label ?workLabel .  FILTER ( lang(?workLabel) = 'en' ) }
}
ORDER BY DESC(?date)
LIMIT 500