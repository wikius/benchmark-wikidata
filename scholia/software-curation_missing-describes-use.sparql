PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?work ?workLabel (CONCAT("/work/", SUBSTR(STR(?work),32)) AS ?workUrl)
  ?softwarename
  (CONCAT(SUBSTR(STR(?work),32), "\tP4510\t{{ q }}\tS887\tQ69652283") AS ?quickStatements)
  (CONCAT("https://quickstatements.toolforge.org/#/v1=",
          SUBSTR(STR(?work),32), "%7CP4510%7C{{ q }}%7CS887%7CQ69652283") AS ?quickStatementsUrl)
WHERE {
  {
  SELECT  ?work ?softwarename WHERE {
    target: wdt:P31 wd:Q7397 ; rdfs:label ?softwarename .
    FILTER(lang(?softwarename) = "en")
    SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:endpoint "www.wikidata.org";
      wikibase:api "Generator";
      mwapi:generator "search";
      mwapi:gsrsearch ?softwarename;
      mwapi:gsrlimit "max".
      ?work wikibase:apiOutputItem mwapi:title.
    }
    ?work wdt:P1476 ?title .
    MINUS {?work wdt:P4510 target: }
    FILTER (REGEX(LCASE(?title), LCASE(CONCAT( "\\\\", "b", ?softwarename ,"\\\\", "b"))))
  } LIMIT 200
} OPTIONAL { ?work rdfs:label ?workLabel .  FILTER ( lang(?workLabel) = 'en' ) }
}