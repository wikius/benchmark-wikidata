PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
  ?count
  ?software ?softwareLabel ?softwareUrl
  ?described_by_example ?described_by_exampleLabel
  ?example_use ?example_useLabel
WHERE {
  {
  SELECT
    (COUNT(DISTINCT ?work) AS ?count)
    ?software
    (CONCAT("/software/", SUBSTR(STR(?software), 32)) AS ?softwareUrl)
    (SAMPLE(?described_by) AS ?described_by_example)
    (SAMPLE(?work) AS ?example_use)
  WHERE {
    ?work wdt:P4510 ?software . 
    ?software wdt:P31/wdt:P279* wd:Q7397 . 
    # Restricting to work takes too long time :(    
    # ?work wdt:P31/wdt:P279* wd:Q386724 .
    OPTIONAL { ?software wdt:P1343 ?described_by . }
  }
  GROUP BY ?software
} OPTIONAL { ?described_by_example rdfs:label ?described_by_exampleLabel .  FILTER ( lang(?described_by_exampleLabel) = 'en' ) }
  OPTIONAL { ?software rdfs:label ?softwareLabel .  FILTER ( lang(?softwareLabel) = 'en' ) }
  OPTIONAL { ?example_use rdfs:label ?example_useLabel .  FILTER ( lang(?example_useLabel) = 'en' ) }
}
ORDER BY DESC(?count)
LIMIT 500    