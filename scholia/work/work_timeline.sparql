PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wdtn: <http://www.wikidata.org/prop/direct-normalized/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#defaultView:Timeline

PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?datetime ?description WHERE {
  {
    target: wdt:P577 ?datetime .
    BIND("üåû publication date" AS ?description)
  }
  UNION 
  {
    target: wdt:P2507 / wdt:P577 ?datetime .
    BIND("‚ùó erratum" AS ?description)
  }
  UNION 
  {
    target: wdt:P5824 / wdt:P577 ?datetime .
    BIND("‚õî retracted" AS ?description)
  }
  UNION 
  {
    target: p:P793 ?event_statement .
    ?event_statement ps:P793 ?event_type .
    ?event_type rdfs:label ?description_ .
    ?event_statement pq:P585 ?datetime .
    FILTER (LANG(?description_) = "en")
    
    # Warning icon for retraction
    BIND(
      IF(
        ?event_type = wd:Q45203135,
        CONCAT("‚õî ", ?description_),
        IF(
          ?event_type = wd:Q56478588,
          CONCAT("‚ùì ", ?description_),
          ?description_
          )
        ) AS ?description)
  }
  UNION
  {
    SELECT ?datetime ?description WHERE {
      target: wdt:P2860 / wdt:P577 ?datetime
      BIND("üìñ‚û°Ô∏è cited work with earliest publication date" AS ?description)
    }
    ORDER BY ?datetime
    LIMIT 1
  }
  UNION
  {
    SELECT ?datetime ?description WHERE {
      target: wdt:P2860 / wdt:P577 ?datetime
      BIND("üìñ‚û°Ô∏è cited work with latest publication date" AS ?description)
    }
    ORDER BY DESC(?datetime)
    LIMIT 1
  }
  UNION
  {
    SELECT ?datetime ?description WHERE {
      target: ^wdt:P2860 / wdt:P577 ?datetime
      BIND("üìñ‚¨ÖÔ∏è citing work with earliest publication date" AS ?description)
    }
    ORDER BY ?datetime
    LIMIT 1
  }
  UNION
  {
    SELECT ?datetime ?description WHERE {
      target: ^wdt:P2860 / wdt:P577 ?datetime
      BIND("üìñ‚¨ÖÔ∏è citing work with latest publication date" AS ?description)
    }
    ORDER BY DESC(?datetime)
    LIMIT 1
  }
    UNION
  {
    target: (wdt:P747 | ^wdt:P629) / wdt:P577 ?datetime
    BIND("üåû Publication of edition" AS ?description)
  }
}